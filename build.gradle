buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
        classpath("com.github.jacobono:gradle-jaxb-plugin:1.3.6")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'
apply plugin: 'com.github.jacobono.jaxb'

jar {
    baseName = 'webservices-bench'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

allprojects {
    sourceCompatibility = 1.8
    eclipse.project {
        natures 'org.eclipse.buildship.core.gradleprojectnature'
    }
}

compileJava {
  options.compilerArgs += [
    '-s', projectDir.absolutePath + '/src/thrift.generated/java',
    '-s', projectDir.absolutePath + '/src/soap.generated/java'
  ]
}

sourceSets {
    main {
        java {
            srcDir "src/thrift.generated/java"
            srcDir "src/soap.generated/java"
        }
    }
}

jaxb { 
  xsdDir = "src/main/resources"
  xjc {
    destinationDir = "src/soap.generated/java"
    generatePackage = "com.github.vlachenal.webservice.bench.soap.api"
  }
}

dependencies {
    // REST 
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("io.springfox:springfox-swagger2:2.7.0")
    compile("io.springfox:springfox-swagger-ui:2.7.0")
    // SQL
    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("org.postgresql:postgresql:42.1.1.jre7")
    // Thrift
    compile("org.apache.thrift:libthrift:0.10.0")
    // SOAP
    compile("org.springframework.boot:spring-boot-starter-web-services")
    compile("wsdl4j:wsdl4j:1.6.1")
    // tests
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    // JAXB task
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41' //jaxws 2.2.6 uses jaxb 2.2.5, but can't dL 2.2.5 from maven the pom is off TODO
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
}