buildscript {
    ext {
        springBootVersion = '2.0.1.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.github.jacobono:gradle-jaxb-plugin:1.3.6")
        classpath("net.ltgt.gradle:gradle-apt-plugin:0.14")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'
apply plugin: 'com.github.jacobono.jaxb'
apply plugin: 'net.ltgt.apt'

war {
    enabled = true
}

ext {
    mapstructVersion = "1.2.0.Final"
}

configurations {
    mapstruct
}

eclipse {
    wtp {
        facet {
            facet name: 'jst.java', version: '10'
            facet name: 'jst.web', version: '4.0'
            def oldJstWebFacet = facets.findAll {
                it.name == 'jst.web' && it.version == '2.4' || it.name == 'jst.java' && it.version == '1.8' || it.name == 'jst.java' && it.version == '1.9' || it.name == 'jst.java' && it.version == '1.10'
            }
            facets.removeAll(oldJstWebFacet)
        }
    }
}

jar {
    baseName = 'webservices-bench'
    version =  '0.1.0'
}

bootRun {
    jvmArgs = ['--add-modules','java.xml.bind,java.xml.ws']
}

repositories {
    mavenCentral()
}

sourceCompatibility = 10
targetCompatibility = 10

allprojects {
    sourceCompatibility = 10
    eclipse.project {
        natures 'org.eclipse.buildship.core.gradleprojectnature'
    }
    // Allows to run JUnit tests in Eclipse and have compilation warning for Mapstruct +
    project.plugins.withId("net.ltgt.apt") {
        // automatically apply net.ltgt.apt-eclipse whenever net.ltgt.apt is used
        project.apply plugin: "net.ltgt.apt-eclipse"
    }
    // Allows to run JUnit tests in Eclipse and have compilation warning for Mapstruct -
}

compileJava {
  options.compilerArgs += [
    '-s', projectDir.absolutePath + '/src/thrift.generated/java',
    '-s', projectDir.absolutePath + '/src/soap.generated/java',
    '-s', projectDir.absolutePath + '/src/protobuf.generated/java'
  ]
}

sourceSets {
    main {
        java {
            srcDir "src/thrift.generated/java"
            srcDir "src/soap.generated/java"
            srcDir "src/protobuf.generated/java"
        }
    }
}

jaxb {
  xsdDir = "src/main/resources"
  xjc {
    destinationDir = "src/soap.generated/java"
    generatePackage = "com.github.vlachenal.webservice.bench.soap.api"
    //header = false
  }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}

dependencies {
    // REST
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'io.springfox:springfox-swagger2:2.8.0'
    compile 'io.springfox:springfox-swagger-ui:2.8.0'
    // SQL
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.postgresql:postgresql:42.2.2'
    // Thrift
    compile 'org.apache.thrift:libthrift:0.11.0'
    // SOAP
    compile 'org.springframework.boot:spring-boot-starter-web-services'
    compile 'wsdl4j:wsdl4j:1.6.3'
    // Dozer
    compile 'com.github.dozermapper:dozer-core:6.2.0'
    // MapStruct
    compile "org.mapstruct:mapstruct-jdk8:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    // tests
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    // Continous integration
    compile 'org.hsqldb:hsqldb:2.4.0'
    // JAXB
    compile "javax.xml.bind:jaxb-api:2.3.0"
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41' //jaxws 2.2.6 uses jaxb 2.2.5, but can't dL 2.2.5 from maven the pom is off TODO
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
    // Protocol buffer
    compile 'com.google.protobuf:protobuf-java:3.5.1'
    compile 'com.google.protobuf:protobuf-java-util:3.5.1'
}
